load("@rules_go//go:def.bzl", "go_binary", "go_library")

filegroup(
    name = "static_srcs",
    srcs = glob(["static/**/*"]),
)

genrule(
    name = "static",
    srcs = [":static_srcs"],
    outs = ["static.tar"],
    cmd = """
    cd louisbuchbinder.com/static
    find . -type l -print | sed 's_^./__' | xargs tar -Lcf ../../$(location static.tar)
    """,
)

external_srcs = [
    "//external/github.com/jgthms/bulma:dist",
    "//external/github.com/FontAwesome/Font-Awesome:dist",
    "//external/github.com/bigskysoftware/htmx:dist",
]

genrule(
    name = "external",
    srcs = external_srcs,
    outs = ["external.tar"],
    cmd = """
    mkdir -p archive/external
    {extract_pkgs}
    cd archive
    find external -type f | xargs \
        tar -cf ../$(location :external.tar)
    """.format(
        extract_pkgs = "\n".join([
            "tar -xf $(location {}) -C archive/external".format(src)
            for src in external_srcs
        ]),
    ),
)

dist_srcs = [
    "//louisbuchbinder.com/build:site",
    ":external",
    ":static",
    "//louisbuchbinder.com/lib:tsc",
    "//wasm:dist",
]

genrule(
    name = "dist",
    srcs = dist_srcs,
    outs = ["dist.tar.gz"],
    cmd = """
    mkdir archive
    {extract_pkgs}
    cd archive
    find . -type f | xargs \
        tar -czf ../$(location :dist.tar.gz)
    """.format(
        extract_pkgs = "\n".join([
            "tar -xf $(location {}) -C archive".format(src)
            for src in dist_srcs
        ]),
    ),
)

genrule(
    name = "manifest_gen",
    srcs = [
        "//louisbuchbinder.com/lib:tsc",
        "//wasm:dist",
    ],
    outs = ["manifest.json"],
    cmd = """
        $(location //lib/build/manifest/gen) \
            --archive $(location //louisbuchbinder.com/lib:tsc) \
            --archive $(location //wasm:dist) \
            --output $(location manifest.json)
    """,
    tools = ["//lib/build/manifest/gen"],
    visibility = ["//visibility:public"],
)

go_library(
    name = "louisbuchbinder_com_lib",
    srcs = ["main.go"],
    importpath = "github.com/louisbuchbinder/core/louisbuchbinder.com",
    visibility = ["//visibility:private"],
    deps = [
        "//lib/util",
        "@rules_go//go/runfiles",
    ],
)

go_binary(
    name = "louisbuchbinder.com",
    data = [
        ":dist",
    ],
    embed = [":louisbuchbinder_com_lib"],
    visibility = ["//visibility:public"],
    x_defs = {
        "distRlocation": "$(rlocationpath :dist)",
    },
)
